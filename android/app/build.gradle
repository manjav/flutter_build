plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    // id "com.google.gms.google-services"
}

// Local Properties
def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader("UTF-8") { reader ->
        localProperties.load(reader)
    }
}
def flutterVersionCode = localProperties.getProperty("flutter.versionCode")
if (flutterVersionCode == null) {
    flutterVersionCode = "1"
}
def flutterVersionName = localProperties.getProperty("flutter.versionName")
if (flutterVersionName == null) {
    flutterVersionName = "1.0"
}

// SDK Properties
def sdkProperties = new Properties()
def sdkPropertiesFile = rootProject.file('sdk.properties')
if (sdkPropertiesFile.exists()) {
    sdkPropertiesFile.withReader('UTF-8') { reader ->
        sdkProperties.load(reader)
    }
}
def minSdkVersion = sdkProperties.getProperty('minSdkVersion').toInteger()
if (minSdkVersion == null) {
    throw new GradleException("minSdkVersion not found in sdk.properties")
}
def targetSdkVersion = sdkProperties.getProperty('targetSdkVersion').toInteger()
if (targetSdkVersion == null) {
    throw new GradleException("targetSdkVersion not found in sdk.properties")
}
def compileSdkVersion = sdkProperties.getProperty('compileSdkVersion').toInteger()
if (compileSdkVersion == null) {
    throw new GradleException("compileSdkVersion not found in sdk.properties")
}
def ndkVersionName = sdkProperties.getProperty('ndkVersion')
if (ndkVersionName == null) {
    throw new GradleException("ndkVersion not found in sdk.properties")
}

// Keystore Properties
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
android {
    namespace = "com.lifetalk.learn.language.speaks"
    ndkVersion = ndkVersionName
    compileSdk = compileSdkVersion
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId = "com.lifetalk.learn.language.speaks"
        minSdk = minSdkVersion
        targetSdk = targetSdkVersion
        versionCode = flutterVersionCode.toInteger()
        versionName = flutterVersionName
    }

    signingConfigs {
       release {
           keyAlias keystoreProperties["keyAlias"]
           keyPassword keystoreProperties["keyPassword"]
           storeFile keystoreProperties["storeFile"] ? file(keystoreProperties["storeFile"]) : null
           storePassword keystoreProperties["storePassword"]
       }
    }
    
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            // proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release 
        }
        debug
        {
            signingConfig signingConfigs.release 
        }
    }
}

flutter {
    source '../..'
}

dependencies {}
